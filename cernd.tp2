
BACKUP ~cernd/backup~
AUTHOR ~Kaeloree and Lava Del'Vortel~
VERSION ~v1.2~
README ~cernd/cerndfriendship-readme-%LANGUAGE%.html~ ~cernd/cerndfriendship-readme-english.html~


// Translations
AUTO_TRA ~cernd/translations/%s~

ALWAYS

	/* Converts tra files to UTF-8 */
	// No exception, everything needs to be converted
	ACTION_DEFINE_ARRAY cerndnoconvert BEGIN END

	// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
	ACTION_DEFINE_ARRAY cerndreload BEGIN cernd END

	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/translations~
			noconvert_array = cerndnoconvert
			reload_array = cerndreload
	END

END


LANGUAGE ~English~
         ~english~
         ~cernd/translations/english/cernd.tra~


/* =================================== *
 *    Cernd Friendship for BG2: ToB    *
 * =================================== */

BEGIN @0 /* Cernd Friendship for BG2: ToB */
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1 /* You must have Throne of Bhaal installed. */
DESIGNATED 0 LABEL ~cernd_friendship~

/* CamDawg's custom IsValidForPartyDialogue() state. Thanks Cam!
   covers STATE_CONFUSED, STATE_FEEBLEMINDED, STATE_SILENCED, STATE_DEAD, STATE_ACID_DEATH, STATE_FLAME_DEATH, STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH, STATE_HELPLESS, STATE_STUNNED, STATE_PANIC, STATE_BERSERK, STATE_SLEEPING */
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

// Compiling dialogue & scripts SOA
COMPILE ~cernd/dialogues/cerndj.d~
EXTEND_TOP ~cernd.bcs~ ~cernd/scripts/cernd.baf~

// Compiling dialogue & scripts TOB
COMPILE ~cernd/dialogues/cernd25j.d~
EXTEND_TOP ~cern25.bcs~ ~cernd/scripts/cern25.baf~
